buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.26.1'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "fr.coppernic.versioning" version "3.1.2"
}

apply plugin: 'com.android.library'
apply plugin: 'io.fabric'
apply plugin: 'checkstyle'

repositories {
    google()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

task checkstyle(type: Checkstyle) {
    configFile rootProject.file('config/checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
}

afterEvaluate {
    if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode versioning.info.versionCode
        versionName versioning.info.display

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            ext.alwaysUpdateBuildId = false
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.jakewharton.timber:timber:4.7.0'

    compileOnly 'org.jetbrains:annotations:15.0'
    compileOnly 'org.slf4j:slf4j-android:1.7.25'
    compileOnly 'io.sentry:sentry-android:1.7.10'
    compileOnly 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.15.0'
    testImplementation "org.robolectric:robolectric:3.8"
    testImplementation "org.awaitility:awaitility:3.0.0"
    testImplementation "org.hamcrest:java-hamcrest:2.0.0.0"
    testImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply from: rootProject.file('gradle/gradle-publish.gradle')
