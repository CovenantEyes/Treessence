apply plugin: 'maven-publish'


def artifactIdHook = { variant ->
    if (variant.buildType.name == "release") {
        return "$project.name"
    } else {
        return "$project.name-$variant.buildType.name"
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

project.android {
    libraryVariants.all { variant ->
        if (variant.buildType.name != "debug") {
            project.publishing.publications.create("${variant.name}", MavenPublication) {
                // Applies the component for the release build variant.
                from components.getByName(variant.name)

                artifact javadocJar
                artifact androidSourcesJar

                // You can then customize attributes of the publication as shown below.
                groupId variant.applicationId
                artifactId "${artifactIdHook(variant)}"
                version versioning.info.display
            }
        }
    }
}
